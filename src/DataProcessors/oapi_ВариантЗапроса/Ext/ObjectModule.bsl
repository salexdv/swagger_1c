#Область ОписаниеПеременных

Перем Данные;
Перем Примеры;
Перем Заголовки;

#КонецОбласти

#Область ПрограммныйИнтерфейс

#Область УправлениеСтруктурой

// Добавляет в вариант запроса данные
//
// Параметры:
//  Данные		 - Прозвольный	 - ТипСтруктура, ТипМассив, ТипСтрока
// 
// Возвращаемое значение:
//  oapi_ВариантЗапроса - ссылка на вариант запроса
//
Функция ДобавитьДанные(Значение) Экспорт
	
	Если Не Сервис.ЭтоРасширенныйТипДанных(Значение) Тогда
		ТекстОшибки = НСтр("ru='В качестве данных должен выступать расширенный тип, например ТипСтруктура, ТипМассив, ТипЧисло.'");
		ВызватьИсключение(ТекстОшибки);
	КонецЕсли;
	
	Данные.Добавить(Значение);
	Возврат ЭтотОбъект;
	
КонецФункции

// Добавляет в вариант запроса заголовок
//
// Параметры:
//  ИмяЗаголовка - Строка	 - Имя заголовка
//  ТипДанных	 - Строка	 - Тип данных заголовка
//  Описание	 - Строка	 - Описание заголовка
// 
// Возвращаемое значение:
//  oapi_ВариантЗапроса - ссылка на вариант запроса
//
Функция ДобавитьЗаголовок(ИмяЗаголовка, ТипДанных = "string", Описание = "") Экспорт
	
	Если Не ЗначениеЗаполнено(ИмяЗаголовка) Тогда
		ВызватьИсключение("Не указано имя заголовка");
	КонецЕсли;
	
	Заголовок = Заголовки[ИмяЗаголовка];
	
	Если Заголовок = Неопределено Тогда
		Заголовок = Обработки.oapi_Заголовок.Создать();
		Заголовок.ИмяЗаголовка = ИмяЗаголовка;
		Заголовок.Сервис = Сервис;
		Заголовок.Владелец = ЭтотОбъект;
		Заголовки[ИмяЗаголовка] = Заголовок;
	КонецЕсли;
	
	Заголовок.Тип      = ТипДанных;
	Заголовок.Описание = Описание;
	
	Возврат ЭтотОбъект;
	
КонецФункции

// Добавляет в вариант запроса пример
//
// Параметры:
//  ИмяПримера		- Строка 		- Имя примера
//  ДанныеПримера	- Произвольный	- ТипСтруктура/Структура, ТипМассив/Массив
// 
// Возвращаемое значение:
//  oapi_ВариантЗапроса - ссылка на вариант запроса
//
Функция ДобавитьПример(ИмяПримера, ДанныеПримера) Экспорт
	
	Примеры[ИмяПримера] = ДанныеПримера;
	Возврат ЭтотОбъект;
	
КонецФункции

// Добавляет в вариант запроса пример
//
// Параметры:
//  ИмяПримера		- Строка 	- Имя примера
//  СтрокаJSON		- Строка	- Строка в формате JSON, содержащая данные примера
// 
// Возвращаемое значение:
//  oapi_ВариантЗапроса - ссылка на вариант запроса
//
Функция ДобавитьПримерJSON(ИмяПримера, СтрокаJSON) Экспорт
	
	Примеры[ИмяПримера] = oapi_ОбщегоНазначения.ДесериализоватьДанные(СтрокаJSON);
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Данные() Экспорт
	
	Возврат Данные;
	
КонецФункции

Функция Примеры() Экспорт
	
	Возврат Примеры;
	
КонецФункции

Функция Заголовки() Экспорт
	
	Возврат Заголовки;
	
КонецФункции

#КонецОбласти

#Область ФормированиеОписания

Функция ПолучитьОписание() Экспорт
	
	ОписанияДанных = Новый Соответствие();
	
	Если ТипДанных = "application/octet-stream" Тогда
		
		ОписаниеСхемы = Новый Структура("type, format", "string", "binary");
		ОписаниеДанных = Новый Структура("schema", ОписаниеСхемы);
		ОписанияДанных.Вставить(ТипДанных, ОписаниеДанных);
		
	Иначе
		
		Для Каждого ДанныеЗапроса Из Данные() Цикл
			
			ОписаниеДанных = oapi_Описание.ПолучитьОписаниеДанных(ДанныеЗапроса, Сервис);
			
			ЕстьСхема = ТипЗнч(ОписаниеДанных) = Тип("Структура") И ОписаниеДанных.Свойство("component");
			ЕстьСхема = ЕстьСхема Или ТипЗнч(ОписаниеДанных) = Тип("Соответствие") И ОписаниеДанных["component"] <> Неопределено;
			Если ЕстьСхема Тогда
				ОписаниеДанных.Удалить("component");
				ОписаниеДанных.Удалить("required");
				ОписаниеДанных.Удалить("type");
				ОписаниеДанных.Удалить("description");
			КонецЕсли;
			
			Если Не Сервис.ИспользоватьСхемыДанных Тогда
				ОписаниеДанных = Новый Структура("schema", ОписаниеДанных);
			КонецЕсли;
			
			Если 0 < Примеры().Количество() Тогда
				
				ОписаниеДанных.Вставить("examples", Новый Соответствие);
				
				Для Каждого Пример Из Примеры() Цикл
					ОписаниеДанных["examples"][Пример.Ключ] = Новый Структура("value", Сервис.ПреобразоватьДанныеВТипы1С(Пример.Значение));
				КонецЦикла;
				
			КонецЕсли;
			
			ОписанияДанных.Вставить(ТипДанных, ОписаниеДанных);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ОписанияДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Инициализация

Данные = Новый Массив();
Примеры = Новый Соответствие();
Заголовки = Новый Соответствие();

#КонецОбласти